package org.opennms.netmgt.correlation.ncs;

import function org.opennms.core.utils.InetAddressUtils.addr;
import function org.opennms.core.utils.InetAddressUtils.str;

import java.util.Date;
import java.net.InetAddress;
import java.util.List;
import java.util.ArrayList;
import java.util.HashSet;


import org.opennms.netmgt.correlation.drools.DroolsCorrelationEngine;
import org.opennms.netmgt.correlation.ncs.*;
import org.opennms.netmgt.model.ncs.*;
import org.opennms.netmgt.xml.event.Event;
import org.opennms.netmgt.xml.event.Parm;
import org.opennms.netmgt.xml.event.Parms;
import org.opennms.netmgt.xml.event.Value;
import org.opennms.netmgt.model.events.EventBuilder;
import org.opennms.netmgt.EventConstants;
import org.opennms.netmgt.capsd.EventUtils;
import org.drools.spi.KnowledgeHelper;

global org.opennms.netmgt.correlation.drools.DroolsCorrelationEngine engine;
global org.opennms.netmgt.correlation.ncs.NCSCorrelationService correlationService;


rule "component down event"
	salience 100
when
	$cde: ComponentDownEvent( $c : component, $e : event )
	not ComponentImpacted ( target == $c )
then
	println( "Component '" + $c.getName() + "' is down!" );
	insert( new ComponentImpacted( $c, $cde ) );
	insert( new DependenciesNeeded( $c, $cde ) ); 
	sendComponentImpactedEvent( engine, $c, $e );
end

rule "component up event"
	salience 100
when
	$cde : ComponentDownEvent( $c : component, $downEvent : event )
	$cue : ComponentUpEvent( component == $c )
then
	println( "Found resolution for " + $cde );
	retract( $cde );
	retract( $cue );
	insert( new ComponentEventResolved( $cde, $cue ) );
end

rule "resolved component"
	salience 1000
when
	$r : ComponentEventResolved( $cause : cause )
	$i : ComponentImpacted( cause == $cause, $c : target )
	$d : DependenciesNeeded( requestor == $cause )
then
	retract( $d );
	retract( $i );
	sendComponentResolvedEvent( engine, $c, $cause.getEvent() );
end

rule "clean up resolutions"
when
	$r : ComponentEventResolved( $c : cause )
	not( ComponentImpacted( cause == $c ) )
then
	println( "Cleanup up resolve " + $r );
	retract( $r );
end

rule "propagate impact"
	when
		ComponentImpacted( $b : target, $cde : cause )
		DependsOn( $parent : a, b == $b )
	
	then
		println( "Parent component '" + $parent.getName() + "' is down!" );
		insert( new ComponentImpacted( $parent, $cde ) );
		insert( new DependenciesNeeded( $parent, $cde ) ); 
		sendComponentImpactedEvent( engine, $parent, $cde.getEvent() );
	
end

function void println(Object msg)
{
	System.err.println(new Date() + " : " + msg);
}


function void sendComponentImpactedEvent( DroolsCorrelationEngine engine, Component c, Event e )
{
	EventBuilder bldr = new EventBuilder( "uei.opennms.org/internal/ncs/componentImpacted", "Component Correlator" );
	bldr.addParam("componentType", c.getType() );
	bldr.addParam("componentName", c.getName() );
	bldr.addParam("componentForeignSource", c.getForeignSource() );
	bldr.addParam("componentForeignId", c.getForeignId() );
	bldr.addParam("cause", e.getDbid() );
	engine.sendEvent(bldr.getEvent());
}


function void sendComponentResolvedEvent( DroolsCorrelationEngine engine, Component c, Event e)
{
	EventBuilder bldr = new EventBuilder( "uei.opennms.org/internal/ncs/componentResolved", "Component Correlator" );
	bldr.addParam("componentType", c.getType() );
	bldr.addParam("componentName", c.getName() );
	bldr.addParam("componentForeignSource", c.getForeignSource() );
	bldr.addParam("componentForeignId", c.getForeignId() );
	bldr.addParam("cause", e.getDbid() );
	engine.sendEvent(bldr.getEvent());
}



